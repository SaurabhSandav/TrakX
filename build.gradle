// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    repositories {
        google()
        mavenCentral()
        jcenter {
            content {
                // AGP
                includeGroup "org.jetbrains.trove4j"

                // Gradle Versions Plugin
                includeGroup "com.github.ben-manes"
            }
        }
    }

    dependencies {
        classpath Android.GRADLE_PLUGIN
        classpath Kotlin.GRADLE_PLUGIN
        classpath Kotlin.SERIALIZATION_PLUGIN
        classpath SQLDelight.GRADLE_PLUGIN
        classpath Navigation.SAFE_ARGS_GRADLE_PLUGIN
        classpath GradleVersionsPlugin.GRADLE_PLUGIN

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {

    repositories {
        google()
        mavenCentral()
        maven { url "https://kotlin.bintray.com/kotlinx" } // TODO (REMOVE) For Serialization
        maven { url "https://jitpack.io" } // For Chucker
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions.freeCompilerArgs += [
                '-progressive',
                '-Xnew-inference',
                '-XXLanguage:+InlineClasses',
                '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi' // TODO (REMOVE) For Kotlin Flow
        ]
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
